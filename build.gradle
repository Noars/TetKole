plugins {
    id 'java'
    id 'application'
    id 'distribution'
    id 'org.openjfx.javafxplugin' version '0.0.10'
    id 'org.beryx.jlink' version '2.24.1'
    id 'com.github.spotbugs' version '5.0.7'
    id "de.undercouch.download" version '4.0.4'
    id 'net.researchgate.release' version '2.6.0'
    id 'com.github.hierynomus.license-report' version '0.16.1'
}

group 'application'

repositories {
    mavenLocal()
    mavenCentral()
}

ext {
    junitVersion = '5.8.1'
}

sourceCompatibility = '17.0.3'
targetCompatibility = '17.0.3'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

application {
    mainClass = 'application.Main'
}

ext.javafxVersion = "17.0.2"
// Make it available everywhere via the project ext:
ext {
    Download = de.undercouch.gradle.tasks.download.Download
    download = download
}

apply from: "${rootDir}/gradle/package.gradle"
apply from: "${rootDir}/gradle/jre.gradle"
apply from: "${rootDir}/gradle/installer.gradle"

javafx {
    version = javafxVersion
    modules = ['javafx.controls', 'javafx.fxml', 'javafx.media']
}

dependencies {

    implementation 'org.projectlombok:lombok:1.18.20'
    implementation 'ws.schild:jave-all-deps:3.3.1'
    implementation 'com.googlecode.json-simple:json-simple:1.1.1'
    implementation 'org.slf4j:slf4j-api:1.7.36'
    testImplementation 'org.slf4j:slf4j-log4j12:1.7.36'
    testImplementation 'org.slf4j:slf4j-simple:1.7.36'
    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")

}

test {
    useJUnitPlatform()
}

spotbugs {
    toolVersion = '4.7.0'
    ignoreFailures = true
}

spotbugsMain {
    reports {
        html {
            required = true
            outputLocation = file("$buildDir/reports/spotbugs/main/spotbugs.html")
            stylesheet = 'fancy-hist.xsl'
        }
    }
}

jlink {
    imageZip = project.file("${buildDir}/distributions/app-${javafx.platform.classifier}.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'app'
    }
}

jlinkZip {
    group = 'distribution'
}

jar {
    enabled=true
    manifest {
        attributes(
                "Implementation-Title": "Cream",
                "Implementation-Vendor": "Univ. Grenoble Alpes - LIG - GETALP",
                "Main-Class": 'application.Main',
                "Class-Path": configurations.runtimeClasspath.collect { it.getName() }.join(' '),
                "JavaFX-Version": javafxVersion,
                "Built-By": System.properties['user.name']
        )
    }
}

distributions {
    windows {
        contents {
            with distributions.main.contents
            from("build/reports")
            from("build/bin/cream-windows.bat") {
                into 'bin'
            }
            from("build/jre/windows") {
                into 'lib/jre'
            }
        }
    }
    linux {
        contents {
            with distributions.main.contents
            from("build/reports")
        }
    }
}

tasks.jar.dependsOn('createLicence')

task createLicence(dependsOn: ['processResources'], type: Copy, group: 'license') {
    from("${rootDir}/gradle/templates/licence.txt")
    expand([
            "date"   : new Date().format('yyyy-MM-dd')
    ])
    into("${processResources.destinationDir}/data/common")
}

task('downloadAndExtractLinuxDependecies').dependsOn('downloadAndExtractLinuxJREwithLibavPlugin')

windowsDistZip.dependsOn(downloadAndExtractWindowsJRE)
linuxDistTar.dependsOn(downloadAndExtractLinuxDependecies)

tasks.withType(Tar){
    compression = Compression.GZIP
    archiveExtension.set('tar.gz')
}

distZip {
    duplicatesStrategy = 'exclude'
}

release {
    failOnPublishNeeded = false
    failOnCommitNeeded = false
}

afterReleaseBuild.dependsOn generateWindowsInstallerInDocker
